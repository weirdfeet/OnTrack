-- id in normal or reverse Tables of rID
UnlockedPointRouteBehaveAlph(id,rID) = union({|switch.id,setRoute.rID,cancelRoute.rID|},{|move.rls, hangMove.rls | rls <- releaseTable(rID,id) |})
UnlockedPointRouteBehave(id,rID) =  -- switch, releasePoint, setRoute, cancelRoute
  (switch.id -> UnlockedPointRouteBehave(id,rID))
  []
  (setRoute.rID ->
    if member(id,union(normalTable(rID),reverseTable(rID))) then
      LockedPointRouteBehave(id,rID)
    else
      UnlockedPointRouteBehave(id,rID))
  []
  ([] rls : releaseTable(rID,id) @ move.rls?t -> UnlockedPointRouteBehave(id,rID))
  []
  ([] rls : releaseTable(rID,id) @ hangMove.rls?t -> UnlockedPointRouteBehave(id,rID))

LockedPointRouteBehave(id,rID) =  -- switch, releasePoint, setRoute, cancelRoute
  (setRoute.rID -> LockedPointRouteBehave(id,rID))
  []
  (cancelRoute.rID -> UnlockedPointRouteBehave(id,rID))
  []
  ([] rls : releaseTable(rID,id) @ move.rls?t -> UnlockedPointRouteBehave(id,rID))
  []
  ([] rls : releaseTable(rID,id) @ hangMove.rls?t -> UnlockedPointRouteBehave(id,rID))

PointRouteBehaveAlph(id) = Union({UnlockedPointRouteBehaveAlph(id,rID) | rID : RouteID})
PointRouteBehave(id) = || rID : RouteID @ [ UnlockedPointRouteBehaveAlph(id,rID) ] UnlockedPointRouteBehave(id,rID)

NormalPointBehaveAlph(id) = {|switch.id, setRoute, ranthru.id, 
                              move.pointAt(id).normalNext(id), move.pointAt(id).reverseNext(id), 
                              move.normalNext(id).pointAt(id), move.reverseNext(id).pointAt(id),
                              hangMove.pointAt(id).normalNext(id), hangMove.pointAt(id).reverseNext(id), 
                              hangMove.normalNext(id).pointAt(id), hangMove.reverseNext(id).pointAt(id) |}
NormalPointBehave(id) =
  (switch.id -> ReversePointBehave(id))
  []
  (move.pointAt(id).normalNext(id) -> NormalPointBehave(id))
  []
  (hangMove.pointAt(id).normalNext(id) -> NormalPointBehave(id))
  []
  (move.normalNext(id).pointAt(id) -> NormalPointBehave(id))
  []
  (hangMove.normalNext(id).pointAt(id) -> NormalPointBehave(id))
  []
  (move.reverseNext(id).pointAt(id) -> ranthru.id -> NormalPointBehave(id))
  []
  (hangMove.reverseNext(id).pointAt(id) -> ranthru.id -> NormalPointBehave(id))
  []
  ([] rID : RouteID @ not(member(id,reverseTable(rID))) & setRoute.rID -> NormalPointBehave(id))

ReversePointBehave(id) =
  (switch.id -> NormalPointBehave(id))
  []
  (move.pointAt(id).reverseNext(id) -> ReversePointBehave(id))
  []
  (hangMove.pointAt(id).reverseNext(id) -> ReversePointBehave(id))
  []
  (move.reverseNext(id).pointAt(id) -> ReversePointBehave(id))
  []
  (hangMove.reverseNext(id).pointAt(id) -> ReversePointBehave(id))
  []
  (move.normalNext(id).pointAt(id) -> ranthru.id -> ReversePointBehave(id))
  []
  (hangMove.normalNext(id).pointAt(id) -> ranthru.id -> ReversePointBehave(id))
  []
  ([] rID : RouteID @ not(member(id,normalTable(rID))) & setRoute.rID -> ReversePointBehave(id))
  
UnoccupiedPointAlpha(id) = {| switch.id, derailed.id, move.t.pointAt(id), move.pointAt(id).t, hangMove.t.pointAt(id), hangMove.pointAt(id).t| t <- diff(TrackID,{pointAt(id)}) |}
UnoccupiedPoint(id) = 
  ([] t : diff(TrackID,{pointAt(id)}) @ move.t.pointAt(id) -> OccupiedPoint(id))
  []
  ([] t : diff(TrackID,{pointAt(id)}) @ move.pointAt(id).t -> UnoccupiedPoint(id))
  []
  ([] t : diff(TrackID,{pointAt(id)}) @ hangMove.t.pointAt(id) -> OccupiedPoint(id))
  []
  ([] t : diff(TrackID,{pointAt(id)}) @ hangMove.pointAt(id).t -> UnoccupiedPoint(id))
  []
  (switch.id -> UnoccupiedPoint(id))

OccupiedPoint(id) = 
  ([] t : diff(TrackID,{pointAt(id)}) @ move.t.pointAt(id) -> OccupiedPoint(id))
  []
  ([] t : diff(TrackID,{pointAt(id)}) @ move.pointAt(id).t -> UnoccupiedPoint(id))
  []
  ([] t : diff(TrackID,{pointAt(id)}) @ hangMove.t.pointAt(id) -> OccupiedPoint(id))
  []
  ([] t : diff(TrackID,{pointAt(id)}) @ hangMove.pointAt(id).t -> UnoccupiedPoint(id))
  []
  (switch.id -> derailed.id -> OccupiedPoint(id))
  
UnoccupiedNormalPointBehaveAlpha(id) = union(NormalPointBehaveAlph(id), UnoccupiedPointAlpha(id))
UnoccupiedNormalPointBehave(id) = NormalPointBehave(id) [ NormalPointBehaveAlph(id) || UnoccupiedPointAlpha(id) ] UnoccupiedPoint(id)

PointBehaveAlpha(id) = union(UnoccupiedNormalPointBehaveAlpha(id), PointRouteBehaveAlph(id))
PointBehave(id) = UnoccupiedNormalPointBehave(id) [ UnoccupiedNormalPointBehaveAlpha(id) || PointRouteBehaveAlph(id) ] PointRouteBehave(id)

Points = || id : PointID @ [PointBehaveAlpha(id)] PointBehave(id)
PointsAlpha = Union({PointBehaveAlpha(id) | id : PointID})
