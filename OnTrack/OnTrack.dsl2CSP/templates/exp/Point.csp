-- id in normal or reverse Tables of rID
UnlockedPointRouteBehaveAlph(id,rID) = union({|switch.id,setRoute.rID,cancelRoute.rID|},{|move.rls | rls <- releaseTable(rID,id) |})
UnlockedPointRouteBehave(id,rID) =  -- switch, releasePoint, setRoute, cancelRoute
  (switch.id -> UnlockedPointRouteBehave(id,rID))
  []
  (setRoute.rID ->
    if member(id,union(normalTable(rID),reverseTable(rID))) then
      LockedPointRouteBehave(id,rID)
    else
      UnlockedPointRouteBehave(id,rID))
  []
  ([] rls : releaseTable(rID,id) @ move.rls?t -> UnlockedPointRouteBehave(id,rID))

LockedPointRouteBehave(id,rID) =  -- switch, releasePoint, setRoute, cancelRoute
  (setRoute.rID -> LockedPointRouteBehave(id,rID))
  []
  (cancelRoute.rID -> UnlockedPointRouteBehave(id,rID))
  []
  ([] rls : releaseTable(rID,id) @ move.rls?t -> UnlockedPointRouteBehave(id,rID))

PointRouteBehaveAlph(id) = Union({UnlockedPointRouteBehaveAlph(id,rID) | rID : RouteID})
PointRouteBehave(id) = || rID : RouteID @ [ UnlockedPointRouteBehaveAlph(id,rID) ] UnlockedPointRouteBehave(id,rID)

NormalPointBehaveAlph(id) = {|switch.id, setRoute, move.pointAt(id).normalNext(id), move.pointAt(id).reverseNext(id) |}
NormalPointBehave(id) =
  (switch.id -> ReversePointBehave(id))
  []
  (move.pointAt(id).normalNext(id) -> NormalPointBehave(id))
  []
  ([] rID : RouteID @ not(member(id,reverseTable(rID))) & setRoute.rID -> NormalPointBehave(id))

ReversePointBehave(id) =
  (switch.id -> NormalPointBehave(id))
  []
  (move.pointAt(id).reverseNext(id) -> ReversePointBehave(id))
  []
  ([] rID : RouteID @ not(member(id,normalTable(rID))) & setRoute.rID -> ReversePointBehave(id))

PointBehaveAlpha(id) = union(NormalPointBehaveAlph(id), PointRouteBehaveAlph(id))
PointBehave(id) = NormalPointBehave(id) [ NormalPointBehaveAlph(id) || PointRouteBehaveAlph(id) ] PointRouteBehave(id)

Points = || id : PointID @ [PointBehaveAlpha(id)] PointBehave(id)
PointsAlpha = Union({PointBehaveAlpha(id) | id : PointID})
